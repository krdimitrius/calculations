5 DIM YH!(2),YW%(2),YI!(2),YA!(2),YN%(2),XW%(2)
10 CLS 
20 ? "êÖòÖçàÖ ëàëíÖåõ ÑàîîÖêÖçñàÄãúçõï ìêÄÇçÖçàâ"
30 ? "(èêéàáÇéÑçõÖ-ëé ëíêéäà 5000)"
40 ? 
50 INPUT "óàëãé ìêÄÇçÖçàâ";N%
60 DIM Y!(N%),W!(N%),A!(N%),C!(N%),D!(N%),E!(N%),F!(N%)
70 INPUT "èéÉêÖòçéëíú ÇõóàëãÖçàâ";E1!
80 INPUT "çÄóÄãúçõâ òÄÉ";H!
90 INPUT "X0";X!
100 FOR J%=1TO N%
110 PRINT "Y0("J%")";
120 INPUT W!(J%)
130 Y!(J%)=W!(J%)
140 NEXT J%
200 SM%=0
210 INPUT "‚†°´®Ê†[0],£‡†¶®™[1],¨´[2]";SM%
220 IF SM%<>2TH300
230 INPUT "àåü";C$
240 OPEN C$+".DAT" FOR OUTPUT
250 C1$="N T "
260 FOR I%=1TO N%
270 C1$=C1$+"P "
280 NEXT I%
290 ? # 32000;N%+2;C1$
300 IF SM%<>1 TH400
302 INPUT "Xmin";XI!
304 INPUT "Xmax";XA!
310 FOR II%=0TO2
320 ? "ÉêÄîàä"II%+1":"
330 INPUT "Ymin";YI!(II%)
340 INPUT "Ymax";YA!(II%)
350 INPUT "Ovar";YN%(II%)
360 NEXT II%
390 GOSUB 9000
400 ZZ%=0
410 GOSUB 4000
420 IF SM%=2TH CLOSE
430 LOCATE 20,0,2
440 END 
4000 ZZ%=ZZ%+1
4010 E3!=0
4020 GOSUB 5000
4030 D%=0
4040 FOR J%=1TO N%
4050 A!(J%)=F!(J%)*H!
4060 Y!(J%)=W!(J%)+A!(J%)/3
4070 NEXT J%
4080 X!=X!+H!/3
4090 GOSUB 5000
4100 FOR J%=1TO N%
4110 Y!(J%)=W!(J%)+(A!(J%)+F!(J%)*H!)/6
4120 NEXT J%
4130 GOSUB 5000
4140 FOR J%=1TO N%
4150 C!(J%)=F!(J%)*H!
4160 Y!(J%)=W!(J%)+A!(J%)/8+.375*C!(J%)
4170 NEXT J%
4180 X!=X!+H!/6
4190 GOSUB 5000
4200 FOR J%=1TO N%
4210 D!(J%)=F!(J%)*H!
4220 Y!(J%)=W!(J%)+A!(J%)*.5-1.5*C!(J%)+2*D!(J%)
4230 NEXT J%
4240 X!=X!+H!*.5
4250 GOSUB 5000
4260 FOR J%=1TO N%
4270 E!(J%)=F!(J%)*H!
4280 Y!(J%)=W!(J%)+(A!(J%)+4*D!(J%)+E!(J%))/6
4290 E2!=ABS(-2*A!(J%)+9*C!(J%)-8*D!(J%)+E!(J%))/30
4300 IF E2!<=E1!TH4330
4310 E3!=1
4320 GOTO 4340
4330 IF E2!<E1!/20TH D%=D%+1
4340 NEXT J%
4350 IF E3!=0TH4420
4360 X!=X!-H!
4370 FOR J%=1TO N%
4380 Y!(J%)=W!(J%)
4390 NEXT J%
4400 H!=H!/2
4410 GOTO 4000
4420 IF D%=N%TH H!=H!+H!
4430 IF SM%<>1 TH ? "N="ZZ%,"X="X!
4440 IF SM%<>2 TH 4470
4450 ? # ZZ%
4460 ? # X!
4470 FOR J%=1 TO N%
4480 IF SM%<>1 TH ? "Y("J%")="Y!(J%)
4490 IF SM%=2 TH ? # Y!(J%)
4500 IF SM%<>1 TH4570
4510 XZ!=X!
4520 FOR Z1%=0TO2
4530 IF J%<>YN%(Z1%)TH4560
4540 YZ!=Y!(J%)
4550 GOSUB 9200
4560 NEXT Z1%
4570 W!(J%)=Y!(J%)
4580 NEXT J%
4590 GOTO 4000
5000 F!(1)=Y!(1)+Y!(2)-X!*X!+X!-2
5010 F!(2)=-2*Y!(1)+4*Y!(2)+2*X!*X!-4*X!-7
5020 RETURN 
9000 IF PEEK(&O40)=0TH ?CHR$(155)
9010 COLOR 2,0
9020 CLS 
9050 XH!=(XA!-XI!)/200
9060 FOR II%=0TO2
9070 YW%(II%)=500
9075 XW%(II%)=500
9080 YH!(II%)=(YA!(II%)-YI!(II%))/200
9090 NEXT II%
9100 FOR II%=0TO200ST20
9110 FOR JJ%=200TO0ST-20
9120 PSET (II%,JJ%),3
9130 NEXT JJ%,II%
9140 RETURN 
9200 XV!=(XZ!-XI!)/XH!
9210 IF XV!<0OR XV!>200TH RETURN
9220 XV%=INT(XV!)+.5
9230 IF XW%(Z1%)>300TH XW%(Z1%)=XV%
9240 YV!=200-(YZ!-YI!(Z1%))/YH!(Z1%)
9250 IF YV!>=0AND YV!<=200.5TH9270EL YW%(Z1%)=500
9260 GOTO 9310
9270 YV%=INT(YV!)
9280 IF YW%(Z1%)>300TH YW%(Z1%)=YV%
9290 LINE (XW%(Z1%),YW%(Z1%))-(XV%,YV%),Z1%+1
9300 YW%(Z1%)=YV%
9310 XW%(Z1%)=XV%
9320 RETURN 
